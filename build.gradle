plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
}

allprojects {
    group = 'com.quackology.duckdevices'
    version = "0.5.0"
    repositories {
        google()
        mavenCentral()
    }
}

def exportedProjects= [
        ":core",
        ":filter"
]

tasks.register('alljavadoc', Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}

tasks.register('allSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from exportedProjects.collect { project(it).sourceSets.main.allJava }
}

subprojects {
    apply plugin: "maven-publish"
    project.afterEvaluate {
        if (!plugins.hasPlugin("android")) {
            publishing {
                publications {
                    maven(MavenPublication) {
                        afterEvaluate {
                            artifactId = "$project.name"
                            if (plugins.hasPlugin("java")) {
                                from components.java
                            } else if (plugins.hasPlugin("android-library")) {
                                from components.release
                            }
                        }
                        repositories {
                            maven {
                                url = "https://maven.pkg.github.com/Vick-Ye/DuckDevices/"
                                credentials {
									username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
									password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
